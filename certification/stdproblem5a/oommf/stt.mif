# MIF 2.1


set pi [expr 4*atan(1.0)]
set mu0 [expr 4*$pi*1e-7]

set basename stt

Parameter Ms 800e3
Parameter A   13e-12

Parameter Happ 0.0 ;# Field in kOe
set Happ [expr {$Happ*1e6/(4*$pi)}]  ;# Convert to A/m

Parameter Happ_angle 180.  ;# Direction of applied field, in degrees
set Happ_angle [expr {$Happ_angle*$pi/180.}]

Parameter mp_theta 1.0 ;# Direction of mp, in degrees
set mp_theta [expr {$mp_theta*$pi/180.}]

set length 160e-9
set width   80e-9
set thick   5e-9

set Nx 64
set Ny 32
set Nz 1

set xcellsize [expr {$length/$Nx}]
set ycellsize [expr {$width/$Ny}]
set zcellsize [expr {$thick/$Nz}]

Specify Oxs_BoxAtlas:atlas [subst {
  xrange {0 $length}
  yrange {0 $width}
  zrange {0 $thick}
}]


Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$xcellsize $ycellsize $zcellsize}
  atlas :atlas
}]

# Exchange
Specify Oxs_UniformExchange [subst {
  A   $A
}]

# Applied field
Specify Oxs_FixedZeeman:Happ [subst {
    field {[expr {cos($Happ_angle)}] [expr {sin($Happ_angle)}] 0.0}
    multiplier $Happ
}]

# Demag
Specify Oxs_Demag {}


# Evolver
Specify Oxs_RungeKuttaEvolve:evolve [subst {
 alpha 1.0
 gamma_G 2.211e5
 method rkf54m
}]
    


# Driver
Specify Oxs_TimeDriver [subst {
 basename [list $basename]
 evolver :evolve
 stopping_dm_dt 1e-5
 stage_count 1
 mesh :mesh
 Ms $Ms
 m0 { Oxs_ScriptVectorField {
    atlas :atlas
    script C_State
    norm 1.0
 }}
}]

proc C_State { x y z } {
    if {$x<0.25} { return [list 0.  -1.  -0.1] }
    if {$x>0.75} { return [list 0.  -1.  -0.1] }
    return [list 1. 0. 0.]
}


Destination archive mmArchive
Schedule Oxs_TimeDriver::Magnetization archive Stage 1

